[TOC]

### Logging

通常，新模块会创建新数据，您可能还希望在新日志文件中收集这些数据。 在Bro中操作日志是一个有用的工具，不仅仅是添加更多的日志文件或字段。 我们使用factorial模块的案例来向您介绍[日志框架](https://www.bro.org/sphinx/frameworks/logging.html)
我们再次考虑factorial.bro。在导出部分中，您会找到一个新行，该行使用redef将名为LOG的新值添加到Log::ID枚举中。这个枚举是Bro的日志框架工作的一部分。你可以在[这里](https://www.bro.org/sphinx-git/scripts/base/frameworks/logging/main.bro.html)找到细节。

下一步是创建一个包含未来日志文件列的记录。该记录被命名为Info。我们创建了两个名为num和factorial_num的列。 num列出n的当前值，factorial_num记录n的阶乘。两个变量都有属性＆log，它告诉Bro必须将给定名称的字段添加到日志流中。

```
#factorial.bro
module Factor;

export {
    # Append the value LOG to the Log::ID enumerable.
    redef enum Log::ID += { LOG };

    # Define a new type called Factor::Info.
    type Info: record {
        num:           count &log;
        factorial_num: count &log;
        };
    global factorial: function(n: count): count;
    }
    
function factorial(n: count): count
    {
    if ( n == 0 )
        return 1;
    
    else
        return ( n * factorial(n - 1) );
    }

```
```
@load factorial

event bro_init()
    {
    # Create the logging stream.
    Log::create_stream(Factor::LOG, [$columns=Factor::Info, $path="factor"]);
    }

event bro_done()
    {
    local numbers: vector of count = vector(1, 2, 3, 4, 5, 6, 7, 8, 9, 10);    
    for ( n in numbers )
        Log::write( Factor::LOG, [$num=numbers[n],
                                  $factorial_num=Factor::factorial(numbers[n])]);
    }

```
![](./images/logging.png)
在本教程中，您已经看到了一些属性。属性用于为函数和变量添加某些属性。例如，＆redef属性允许重新定义全局常量或扩展类型。 ＆optional允许记录字段中的值丢失为空值。[这里](https://www.bro.org/sphinx/script-reference/attributes.html?highlight=attributes)找到所有属性的列表。

现在请切换到文件main.bro。在我们脚本的开始处，我们需要创建新的日志记录流。 [Log::create_stream](https://www.bro.org/sphinx/scripts/base/frameworks/logging/main.bro.html?highlight=log%3A%3Acreate_stream#id-Log::create_stream)的作用就是创建日志流。必要的参数当然是模块的LOG值，以及保存日志记录字段的记录。变量$path告诉Bro如何命名新的日志文件。请注意，日志流需要在bro_init事件中进行初始化。

下一步看起来与之前的非常相似，但不是将结果打印到Stdout，而是使用[Log::write](https://www.bro.org/sphinx/scripts/base/frameworks/logging/main.bro.html?highlight=log%3A%3Awrite#id-Log::write)函数写入新日志。

关于写入日志的更多注意事项：在本例中，我们在bro_done中编写了所有结果，在现实世界的例子中，这通常在与日志文件相关的事件处理程序内完成。
* [Log::ID](https://www.bro.org/sphinx/scripts/base/frameworks/broker/main.bro.html#type-Log::ID)
* [Log::Stream](https://www.bro.org/sphinx/scripts/base/frameworks/logging/main.bro.html?highlight=log%3A%3Acreate_stream#type-Log::Stream)

```
Type:record

columns: any
A record type defining the log’s columns.

ev: any &optional
Event that will be raised once for each log entry. The event receives a single same parameter, an instance of type columns.

path: string &optional
A path that will be inherited by any filters added to the stream which do not already specify their own path.

Type defining the content of a logging stream.
```