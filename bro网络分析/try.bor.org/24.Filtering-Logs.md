[TOC]
如上一课所述，过滤可以通过多种方式操纵Bro的日志记录。重命名日志文件是一个。将流分成两个日志文件是我们在本课中探索的一个。

为此，我们回到factor模块。现在的目标是将记录流分成两部分。我们将所有结果发送到一个可以被5整除的日志文件，其他记录到另一个日志文件。根据模函数动态决定哪个记录发送到哪个日志文件。在[这里](https://www.bro.org/sphinx/frameworks/logging.html#determine-log-path-dynamically)查找更多关于动态确定日志路径的细节

现在运行代码示例。您会看到现在有两个日志文件，一个名为num和factorial_num。让我们看看代码。在模块factorial.bro中，我们添加一个新函数，即我们将要使用的path_func。该函数也被添加到导出部分。函数返回的数字是否在我们的模5类中，然后返回一个字符串。返回的字符串是两个新日志文件的名称。在main.bro中，您会发现我们在bro_init中添加了更多行。我们创建一个名为split-mod5s的过滤器，添加新过滤器并删除默认过滤器。注释掉第9行，删除默认值并查看会发生什么。

```
module Factor;

export {
    # Append the value LOG to the Log::ID enumerable.
    redef enum Log::ID += { LOG };

    # Define a new type called Factor::Info.
    type Info: record {
        num:           count &log;
        factorial_num: count &log;
        };
    global factorial: function(n: count): count;
    global mod5: function(id: Log::ID, path: string, rec: Factor::Info) : string;
    }
    
function factorial(n: count): count
    {
    if ( n == 0 )
        return 1;
    
    else
        return ( n * factorial(n - 1) );
    }
    
function mod5(id: Log::ID, path: string, rec: Factor::Info) : string    
    {
    if ( rec$factorial_num % 5 == 0 )
        return "factor-mod5";
    
    else
        return "factor-non5";
    }

```
```
@load factorial

event bro_init()
    {
    Log::create_stream(Factor::LOG, [$columns=Factor::Info, $path="factor"]);
    
    local filter: Log::Filter = [$name="split-mod5s", $path_func=Factor::mod5];
    Log::add_filter(Factor::LOG, filter);
    Log::remove_filter(Factor::LOG, "default");
    }

event bro_done()
    {
    local numbers: vector of count = vector(1, 2, 3, 4, 5, 6, 7, 8, 9, 10);    
    for ( n in numbers )
        Log::write( Factor::LOG, [$num=numbers[n],
                                  $factorial_num=Factor::factorial(numbers[n])]);
    }

```