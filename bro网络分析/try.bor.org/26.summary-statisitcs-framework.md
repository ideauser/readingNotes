[TOC]

### 摘要统计框架

摘要统计框架（short sumstats）是深入了解网络中发生的事情的非常有用的工具。扫描检测器就是一个很好的例子，它可以将许多短暂的，甚至可能是失败的连接看起来无关，并将其联系起来，并给出有关扫描仪活动的警告。对许多用户来说，这个框架似乎很难使用。本教程旨在改变这一点。

除了本教程，您还可以阅读解释[Sumstats框架](https://www.bro.org/sphinx/frameworks/sumstats.html)所用术语的文档。本教程遵循（部分）现场教程Seth Hall在BroCon 2014上提供的，该[教程](https://www.youtube.com/watch?v=9YsenekNaSI&feature=youtu.be)可作为Youtube视频提供

将代码运行到左侧并查看输出。现在让我们通过代码。

首先sumstats需要计数，即一个bro事件。对于这个例子，我们选择了事件“dns_request”，我们将计算唯一的DNS请求，这是每个主机的唯一请求。 “if”行用于确保真正的DNS请求（通过端口53）也不为空。这种特殊情况会迅速耗尽您的Bro系统的内存，因为它为每个可以看到的主机收集每个唯一的DNS请求，因此一种选择是将其限制为端口53，非空请求和仅限本地主机。如果这是一个真实的请求，我们希望观察某些东西，那就是使用sumstats观察者的地方。字符串dns.lookup是一个任意的名字，它可能是别的。原则是观察（计数）一个信息流，该流得到一个名称，以便稍后处理。下一部分是关键，在这种情况下是发送请求的主机。要知道实际的唯一请求，查询字符串也需要成为关键的一部分。

现在有一条观察到的流，下一步是减少（即汇总）流，然后对其进行处理。 reducer获取一个变量名称，在这种情况下，r1被附加到名为dns.lookup的流，并且还需要至少一个应用于流的reduce函数。在这个例子中，使用的方法是“UNIQUE”。可以应用多种计算方法，它们都列在[sumstats参考](https://www.bro.org/sphinx/scripts/base/frameworks/sumstats/main.bro.html#type-SumStats::Calculation)中。还可以有多个reducer，在[MIME类型统计](https://www.bro.org/sphinx/mimestats/index.html)的文档中解释了一个示例。

第三步是将reducer与SumStats连接起来，最后做一些事情。 SumStats也获得一个名称以供稍后参考。分配一个时间段，在这种情况下是6个小时。 6小时后或跟踪结束后，将返回结果。reducer（或一组还原剂）已连接。回调函数是时间结果，这是观察者之前给出的关键，它会给你结果。现在您可以访问名为dns.lookup的流的结果。在这种情况下，我们打印主机，DNS请求总数和唯一请求数。

注释掉epoch_result中的两行并直接打印结果。您可以看到所有可用于打印的字段。

练习：使用[独特计算的启发式版本HLL_UNIQUE](https://www.bro.org/sphinx/scripts/base/frameworks/sumstats/main.bro.html#type-SumStats::Calculation)，您可以在文档中找到它。然后取一个大小为5的样本。有关解决方案，请转到下一页。

```
@load base/frameworks/sumstats
event bro_init()
    {
    local r1 = SumStats::Reducer($stream="dns.lookup", $apply=set(SumStats::UNIQUE));
    SumStats::create([$name="dns.requests.unique",
                      $epoch=6hrs,
                      $reducers=set(r1),
                      $epoch_result(ts: time, key: SumStats::Key, result: SumStats::Result) =
                        {
                        local r = result["dns.lookup"];
                        print fmt("%s did %d total and %d unique DNS requests in the last 6 hours.", 
                        			key$host, r$num, r$unique);
                        }]);
    }

event dns_request(c: connection, msg: dns_msg, query: string, qtype: count, qclass: count)
    {
    if ( c$id$resp_p == 53/udp && query != "" )
        SumStats::observe("dns.lookup", [$host=c$id$orig_h], [$str=query]);
    }

```

```
192.168.1.102 did 17 total and 6 unique DNS requests in the last 6 hours.
192.168.1.103 did 18 total and 6 unique DNS requests in the last 6 hours.
192.168.1.105 did 37 total and 20 unique DNS requests in the last 6 hours.
192.168.1.102 did 6 total and 3 unique DNS requests in the last 6 hours.
192.168.1.104 did 89 total and 86 unique DNS requests in the last 6 hours.
192.168.1.103 did 48 total and 42 unique DNS requests in the last 6 hours.
```